#version 460 core

#define SPLAT 1

layout (local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform writeonly image2D color_tex;

uniform int screen_w;
uniform int quilt_cols;

layout(std430, binding = 0) buffer storage_buffer
{
    uint col_depth[];
};

void main()
{  	
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);

	// calculate index for looking up values in the storage buffer
	// no further calculations for the placement of the point have to be made because storage buffer is constructed like quilt
	int index = coord.x+coord.y*(screen_w*quilt_cols);
	uint color_at_point = col_depth[index];

	// do point splatting if the current pixel is not visible --> use left and right point in x-direction and interpolate their values
	uint mask = ~(0xffffffff << 8);
	#if SPLAT == 1
		uint visible = (color_at_point & (1 << 24)) >> 24;
		if (visible == 0){
			int left = index-1; 
			int right = index+1;
			// edge cases at left and right side of each view so that interpolation is not done across different views
			if (coord.x%screen_w == 0)
				left=index;
			else if (coord.x%screen_w == screen_w-1)
				right = index;
			uint color_int_left = col_depth[left];
			uint color_int_right = col_depth[right];

			// getting colors from storage buffer integer from: https://developer.download.nvidia.com/cg/bitfieldExtract.html and immedieately interpolate them
			float alpha = (((color_int_left & (1 << 24)) >> 24) + ((color_int_right & (1 << 24)) >> 24))/2.0;
			float red = (((color_int_left >> 16) & mask) + ((color_int_right >> 16) & mask))/500.0;
			float green = (((color_int_left >> 8) & mask) + ((color_int_right >> 8) & mask))/500.0;
			float blue = ((color_int_left& mask) + (color_int_right & mask))/500.0;

			if (alpha != 0)
				imageStore(color_tex, coord, vec4(red, green, blue,1));
		}
		// if the pixel is visible, directly store the point in the image
		else{
			// getting colors from storage buffer integer from: https://developer.download.nvidia.com/cg/bitfieldExtract.html
			float red = ((color_at_point >> 16) & mask)/255.0;
			float green = ((color_at_point >> 8) & mask)/255.0;
			float blue = (color_at_point & mask)/255.0;

			imageStore(color_tex, coord, vec4(red, green, blue,1));
		}
	// no point splatting
	#else
		// getting colors from storage buffer integer from: https://developer.download.nvidia.com/cg/bitfieldExtract.html
		uint alpha = (color_at_point & (1 << 24)) >> 24;
		float red = ((color_at_point >> 16) & mask)/255.0;
		float green = ((color_at_point >> 8) & mask)/255.0;
		float blue = (color_at_point & mask)/255.0;

		if (alpha != 0)
			imageStore(color_tex, coord, vec4(red,green,blue,1.0));
	#endif
}