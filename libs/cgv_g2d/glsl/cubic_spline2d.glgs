#version 430

#define MODE 1

//***** begin interface of shape2d.glsl ***********************************
float get_feather_width();
vec2 get_shape_size();
vec4 get_color();
vec4 transform_world_to_window_space(vec2 p);
//***** end interface of shape2d.glsl ***********************************

#if MODE == 0
layout(points) in;
#else
layout(lines) in;
#endif
layout(triangle_strip, max_vertices = 8) out;

struct cubic_node {
	vec2 position;
	vec2 tangent;
};

struct cubic_segment {
	cubic_node s;
	cubic_node e;
};

struct quadratic_segment {
	vec2 s;
	vec2 h;
	vec2 e;
};

const vec2 corners[4] = vec2[](
	vec2(0.0, 0.0),
	vec2(1.0, 0.0),
	vec2(0.0, 1.0),
	vec2(1.0, 1.0)
);

uniform float width = 1.0;

#if MODE == 0
flat in vec4 position23_gs[];
#endif
flat in vec4 color_gs[];

out flat vec4 color0_fs;
out flat vec4 color1_fs;
out flat vec2 size_fs;
out vec2 tex_coord_fs;
out vec2 pix_coord_fs;
out flat vec2 pos_a_fs;
out flat vec2 pos_b_fs;
out flat vec2 pos_c_fs;
out flat vec2 pos_d_fs;
out flat vec2 pos_e_fs;

#define DEF_split_curve(T)						\
void split_curve(T v0, T d0, T v1, T d1,		\
	out T v00_out, out T h0_out, out T v10_out,	\
	out T v01_out, out T h1_out, out T v11_out)	\
{												\
	v00_out = v0;								\
	h0_out = v0 + d0 * 0.25;					\
	T h1 = v1 - d1 * 0.25;						\
	v10_out = (h0_out + h1) * 0.5;				\
												\
	v01_out = v10_out;							\
	h1_out = h1;								\
	v11_out = v1;								\
}

DEF_split_curve(vec2)

#define SPLIT_CURVE(TUBE, MEM_V, MEM_D, QTUBE0, QTUBE1)		\
split_curve(												\
	TUBE.s.MEM_V, TUBE.s.MEM_D, TUBE.e.MEM_V, TUBE.e.MEM_D,	\
	QTUBE0.s, QTUBE0.h, QTUBE0.e,							\
	QTUBE1.s, QTUBE1.h, QTUBE1.e							\
)

void split_cubic_segment(in cubic_segment cseg, out quadratic_segment qseg0, out quadratic_segment qseg1) {
	SPLIT_CURVE(cseg, position, tangent, qseg0, qseg1);
}

vec2 ortho2d(vec2 v) {
	return vec2(v.y, -v.x);
}

// https://www.shadertoy.com/view/lsyfWc
vec4 bbox_bezier(in vec2 p0, in vec2 p1, in vec2 p2) {
    // extremes
    vec2 mi = min(p0,p2);
    vec2 ma = max(p0,p2);

    // maxima/minima point, if p1 is outside the current bbox/hull
    if( p1.x<mi.x || p1.x>ma.x || p1.y<mi.y || p1.y>ma.y )
    {
        // p = (1-t)^2*p0 + 2(1-t)t*p1 + t^2*p2
        // dp/dt = 2(t-1)*p0 + 2(1-2t)*p1 + 2t*p2 = t*(2*p0-4*p1+2*p2) + 2*(p1-p0)
        // dp/dt = 0 -> t*(p0-2*p1+p2) = (p0-p1);

        vec2 t = clamp((p0-p1)/(p0-2.0*p1+p2),0.0,1.0);
        vec2 s = 1.0 - t;
        vec2 q = s*s*p0 + 2.0*s*t*p1 + t*t*p2;
        
        mi = min(mi,q);
        ma = max(ma,q);
    }
    
    return vec4( mi, ma );
}

void emit_bounding_box(in quadratic_segment qseg0, in quadratic_segment qseg1) {

	vec4 box0 = bbox_bezier(qseg0.s, qseg0.h, qseg0.e);
	vec4 box1 = bbox_bezier(qseg1.s, qseg1.h, qseg1.e);

	vec4 box;
	box.xy = min(box0.xy, box1.xy);
	box.zw = max(box0.zw, box1.zw);

#if MODE == 0
	color0_fs = color_gs[0];
	color1_fs = color_gs[0];
#else
	color0_fs = color_gs[0];
	color1_fs = color_gs[1];
#endif

	float final_width = width + get_feather_width();

	// incorporate feather_width
	box.xy -= 0.5*final_width;
	box.zw += 0.5*final_width;

	size_fs = box.zw - box.xy;

	vec2 org = box.xy;

	tex_coord_fs = corners[0];
	pix_coord_fs = size_fs*corners[0] + org;
	gl_Position = transform_world_to_window_space(box.xy);
	EmitVertex();
	tex_coord_fs = corners[1];
	pix_coord_fs = size_fs*corners[1] + org;
	gl_Position = transform_world_to_window_space(box.zy);
	EmitVertex();

	tex_coord_fs = corners[2];
	pix_coord_fs = size_fs*corners[2] + org;
	gl_Position = transform_world_to_window_space(box.xw);
	EmitVertex();
	tex_coord_fs = corners[3];
	pix_coord_fs = size_fs*corners[3] + org;
	gl_Position = transform_world_to_window_space(box.zw);
	EmitVertex();
}

/*void emit_bounding_box(in int idx, in quadratic_segment qseg) {

	vec4 box = bbox_bezier(qseg.s, qseg.h, qseg.e);

	idx_fs = idx;
	color_fs = color_gs[0];

	float final_width = width + feather_width;

	// incorporate feather_width
	box.xy -= 0.5*final_width;
	box.zw += 0.5*final_width;

	size_fs = box.zw - box.xy;

	vec2 org = box.xy;

	tex_coord_fs = corners[0];
	pix_coord_fs = size_fs*corners[0] + org;
	gl_Position = to_window_space(box.xy);
	EmitVertex();
	tex_coord_fs = corners[1];
	pix_coord_fs = size_fs*corners[1] + org;
	gl_Position = to_window_space(box.zy);
	EmitVertex();

	tex_coord_fs = corners[2];
	pix_coord_fs = size_fs*corners[2] + org;
	gl_Position = to_window_space(box.xw);
	EmitVertex();
	tex_coord_fs = corners[3];
	pix_coord_fs = size_fs*corners[3] + org;
	gl_Position = to_window_space(box.zw);
	EmitVertex();
}*/

void main() {
	
	cubic_segment cseg;

#if MODE == 0
	cseg.s.position = gl_in[0].gl_Position.xy;
	cseg.s.tangent = gl_in[0].gl_Position.zw;
	cseg.e.position = position23_gs[0].xy;
	cseg.e.tangent = position23_gs[0].zw;
#else
	cseg.s.position = gl_in[0].gl_Position.xy;
	cseg.s.tangent = gl_in[0].gl_Position.zw;
	cseg.e.position = gl_in[1].gl_Position.xy;
	cseg.e.tangent = gl_in[1].gl_Position.zw;
#endif

	quadratic_segment qseg0, qseg1;
	split_cubic_segment(cseg, qseg0, qseg1);
	
	pos_a_fs = qseg0.s;
	pos_b_fs = qseg0.h;
	pos_c_fs = qseg0.e;
	pos_d_fs = qseg1.h;
	pos_e_fs = qseg1.e;

	emit_bounding_box(qseg0, qseg1);

	//emit_bounding_box(0, qseg0);
	//emit_bounding_box(1, qseg1);
}
